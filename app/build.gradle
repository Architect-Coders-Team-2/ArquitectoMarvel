plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'kotlin-parcelize'
    id 'dagger.hilt.android.plugin'
}

def secretProperties() {
    def file = file("secret.properties")
    def properties = new Properties()
    properties.load(new FileInputStream(file))
    return properties
}

android {
    compileSdkVersion 31
    buildToolsVersion "31.0.0"

    defaultConfig {
        applicationId "com.architectcoders.arquitectomarvel"
        minSdkVersion 21
        targetSdkVersion 31
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "com.architectcoders.arquitectomarvel.CustomTestRunner"
    }

    def MARVEL_API_KEY = 'MARVEL_API_KEY'
    def MARVEL_PRIVATE_KEY = 'MARVEL_PRIVATE_KEY'

    def apikey = "default-apikey"
    def privatekey = "default-privatekey"
    try {
        apikey = secretProperties()[MARVEL_API_KEY]
        privatekey = secretProperties()[MARVEL_PRIVATE_KEY]
    } catch (FileNotFoundException exception) {
        println(exception.message)
        println("This exception is handled and intended to be thrown when github actions runs \"gradle build\" on the project.")
    }

    buildTypes {
        release {
            minifyEnabled false
            buildConfigField("String", MARVEL_API_KEY, "\"" + apikey + "\"")
            buildConfigField("String", MARVEL_PRIVATE_KEY, "\"" + privatekey + "\"")
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            minifyEnabled false
            buildConfigField("String", MARVEL_API_KEY, "\"" + apikey + "\"")
            buildConfigField("String", MARVEL_PRIVATE_KEY, "\"" + privatekey + "\"")
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions "version"
    productFlavors {
        prod {
            dimension "version"
            versionNameSuffix "-prod"
        }
        idlingResources {
            dimension "version"
            versionNameSuffix "-idlingResources"
        }
    }

    variantFilter { variant ->
        if(variant.flavors*.name.contains("idlingResources") && variant.buildType*.name.contains("release")) {
            setIgnore(true)
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    kotlinOptions {
        jvmTarget = '11'
    }

    buildFeatures {
        viewBinding true
    }

    hilt {
        enableAggregatingTask = true
    }
}

dependencies {
    implementation project(":usecases")
    implementation project(":data")
    implementation project(":domain")

    implementation androidLibs.values()
    implementation kotlinLibs.values()
    implementation libs.values()
    kapt androidKaptLibs.values()

    testImplementation testLibsForAllModules.values()
    testImplementation testLibsForApp.values()
    androidTestImplementation androidTestLibs.values()
    androidTestImplementation("org.jetbrains.kotlinx:kotlinx-coroutines-test:1.5.2") {
        // conflicts with mockito due to direct inclusion of byte buddy
        exclude group: "org.jetbrains.kotlinx", module: "kotlinx-coroutines-debug"
    }
    kaptTest kaptTestLibs.values()
    kaptAndroidTest kaptAndroidTestLibs.values()
}

kapt {
    correctErrorTypes = true
}